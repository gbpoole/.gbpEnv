
# Decide what sort of system we're on
if ( -f /bin/uname ) then
  setenv GBP_OS `/bin/uname`
else
  setenv GBP_OS 'unknown'
endif
if ( $HOSTTYPE == 'intel-mac' ) then
  setenv GBPHOST 'Mac'
else if ( $HOSTTYPE == 'x86_64-linux' ) then
  setenv GBPHOST 'linux'
else
  setenv GBPHOST 'unknown'
  echo 'Unknown System Type {'$HOSTTYPE'}.'
endif

# Set-up some generic UNIX stuff
umask 22
limit coredumpsize 0
unset autologout

# Make sure the DISPLAY variable is set
if (!(${?DISPLAY}) && ${?REMOTEHOST}) then
   setenv DISPLAY ${REMOTEHOST}:0.0
endif

# Set all system-specific stuff
source ${HOME}/.system.gbpHome

# Create all my aliases
source ${HOME}/.alias.gbpHome

# Add 3rd_Party binary directory to the PATH
setenv PATH  ${HOME}/3rd_Party/bin/:$PATH

# These lines set-up gbpCode
setenv GBP_SRC  ${HOME}'/gbpCode/'
source ${GBP_SRC}/.cshrc.gbpCode
setenv PATH  ${GBP_SRC}/myBin:$PATH

# Set the prompt
setprompt

